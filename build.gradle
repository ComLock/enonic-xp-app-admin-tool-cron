plugins {
    id 'java' // Needed for assemble and build
    id 'maven'
    id 'com.enonic.xp.app' version '1.0.17'
    id 'com.moowork.node' version '1.2.0'
}


repositories {
    mavenLocal()
    jcenter()
    xp.enonicRepo()
}


app {
    name = project.appName
    displayName = 'Cron admin'
    vendorName = 'Enonic AS'
    vendorUrl = 'http://enonic.com'
    systemVersion = "${xpVersion}"
}


dependencies {
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"

    include 'com.enonic.lib:cron:1.0.0-SNAPSHOT'

    include 'com.enonic.lib:util:1.3.0'
}


node {
    version '8.11.2'
    download true
}


sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources { // Paths relative to src/main/resources
            exclude '**/*.es' // Avoid having your es files copied into ./build
        }
    }
}


task webpack(type:NodeTask) {
    script = file('node_modules/webpack-cli/bin/webpack.js')
    args = [
        '--color',
        '--config', 'webpack.config.babel.js'
    ]
}
webpack.dependsOn npmInstall
assemble.dependsOn webpack
assemble.mustRunAfter webpack


task watch(type:NodeTask) {
    script = file('node_modules/webpack-cli/bin/webpack.js')
    args = [
        '--color',
        '--config', 'webpack.config.babel.js',
        '--watch'
    ]
}
